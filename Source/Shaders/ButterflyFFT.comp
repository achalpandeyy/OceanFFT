#version 460 core

layout (local_size_x = 32, local_size_y = 32) in;

// Todo: These binding points could pose a problem
layout (binding = 0, rgba32f) readonly uniform image2D u_ButterflyPrecomp;
layout (binding = 1, rgba32f) uniform image2D u_PingPong0;
layout (binding = 2, rgba32f) uniform image2D u_PingPong1;

uniform int u_PingPongIndex;
uniform int u_FFTDirection;
uniform int u_Stage;

struct Complex
{
	float real;
	float im;
};

Complex Add(Complex c1, Complex c2)
{
	Complex result = Complex(c1.real + c2.real, c1.im + c2.im);
	return result;
}

Complex Multiply(Complex c1, Complex c2)
{
	Complex result = Complex(c1.real * c2.real - c1.im * c2.im, c1.real * c2.im + c1.im * c2.real);
	return result;
}

void HorizontalButterflies(in ivec2 pixel_pos)
{
	vec4 butterfly_precomp = imageLoad(u_ButterflyPrecomp, ivec2(u_Stage, pixel_pos.x));

	if (u_PingPongIndex == 0)
	{
		vec2 a_ = imageLoad(u_PingPong0, ivec2(butterfly_precomp.z, pixel_pos.y)).rg;
		vec2 b_ = imageLoad(u_PingPong0, ivec2(butterfly_precomp.w, pixel_pos.y)).rg;

		Complex a = Complex(a_.x, a_.y);
		Complex b = Complex(b_.x, b_.y);
		Complex twiddle_factor = Complex(butterfly_precomp.x, butterfly_precomp.y);

		// Buttefly operation
		Complex result = Add(a, Multiply(twiddle_factor, b));

		imageStore(u_PingPong1, pixel_pos, vec4(result.real, result.im, 0.f, 1.f));
	}
	else if (u_PingPongIndex == 1)
	{
		vec2 a_ = imageLoad(u_PingPong1, ivec2(butterfly_precomp.z, pixel_pos.y)).rg;
		vec2 b_ = imageLoad(u_PingPong1, ivec2(butterfly_precomp.w, pixel_pos.y)).rg;

		Complex a = Complex(a_.x, a_.y);
		Complex b = Complex(b_.x, b_.y);
		Complex twiddle_factor = Complex(butterfly_precomp.x, butterfly_precomp.y);

		// Buttefly operation
		Complex result = Add(a, Multiply(twiddle_factor, b));

		imageStore(u_PingPong0, pixel_pos, vec4(result.real, result.im, 0.f, 1.f));
	}
}

void VerticalButterfiles(in ivec2 pixel_pos)
{
	vec4 butterfly_precomp = imageLoad(u_ButterflyPrecomp, ivec2(u_Stage, pixel_pos.y));

	if (u_PingPongIndex == 0)
	{
		vec2 a_ = imageLoad(u_PingPong0, ivec2(pixel_pos.x, butterfly_precomp.z)).rg;
		vec2 b_ = imageLoad(u_PingPong0, ivec2(pixel_pos.x, butterfly_precomp.w)).rg;

		Complex a = Complex(a_.x, a_.y);
		Complex b = Complex(b_.x, b_.y);
		Complex twiddle_factor = Complex(butterfly_precomp.x, butterfly_precomp.y);

		// Buttefly operation
		Complex result = Add(a, Multiply(twiddle_factor, b));

		imageStore(u_PingPong1, pixel_pos, vec4(result.real, result.im, 0.f, 1.f));
	}
	else if (u_PingPongIndex == 1)
	{
		vec2 a_ = imageLoad(u_PingPong1, ivec2(pixel_pos.x, butterfly_precomp.z)).rg;
		vec2 b_ = imageLoad(u_PingPong1, ivec2(pixel_pos.x, butterfly_precomp.w)).rg;

		Complex a = Complex(a_.x, a_.y);
		Complex b = Complex(b_.x, b_.y);
		Complex twiddle_factor = Complex(butterfly_precomp.x, butterfly_precomp.y);

		// Buttefly operation
		Complex result = Add(a, Multiply(twiddle_factor, b));

		imageStore(u_PingPong0, pixel_pos, vec4(result.real, result.im, 0.f, 1.f));
	}
}

void main()
{
	ivec2 pixel_pos = ivec2(gl_GlobalInvocationID.xy);
	if (u_FFTDirection == 0) HorizontalButterflies(pixel_pos);
	else if (u_FFTDirection == 1) VerticalButterfiles(pixel_pos);
	else
	{
		imageStore(u_PingPong0, pixel_pos, vec4(1.f, 0.f, 1.f, 1.f));
		imageStore(u_PingPong1, pixel_pos, vec4(1.f, 0.f, 1.f, 1.f));
	}
}